scoreboard players operation @s lastBiome = @s currentBiome
scoreboard players set @s currentBiome 0

# Echoing Forest
execute at @s positioned -133 ~ 247 in the_end run scoreboard players set @s[r=172] currentBiome 1
execute at @s positioned -57 ~ 399 in the_end run scoreboard players set @s[r=130] currentBiome 1
execute at @s positioned 122 ~ 303 in the_end run scoreboard players set @s[r=195] currentBiome 1

# Ender Riftlands
execute at @s positioned -490 ~ -530 in the_end run scoreboard players set @s[r=310] currentBiome 2
execute at @s positioned -65 ~ -645 in the_end run scoreboard players set @s[r=180] currentBiome 2
execute at @s positioned 502 ~ -100 in the_end run scoreboard players set @s[r=250] currentBiome 2
execute at @s positioned 545 ~ 330 in the_end run scoreboard players set @s[r=270] currentBiome 2
execute at @s positioned 240 ~ 574 in the_end run scoreboard players set @s[r=130] currentBiome 2
execute at @s positioned 188 ~ 732 in the_end run scoreboard players set @s[r=130] currentBiome 2
execute at @s positioned 169 ~ 861 in the_end run scoreboard players set @s[r=100] currentBiome 2
execute at @s positioned 18 ~ 770 in the_end run scoreboard players set @s[r=200] currentBiome 2
execute at @s positioned -184 ~ 864 in the_end run scoreboard players set @s[r=120] currentBiome 2
execute at @s positioned -203 ~ 673 in the_end run scoreboard players set @s[r=120] currentBiome 2

# Ender Abyss
execute at @s positioned 309 ~ -558 in the_end run scoreboard players set @s[r=208] currentBiome 3
execute at @s positioned -459 ~ 433 in the_end run scoreboard players set @s[r=218] currentBiome 3
execute at @s positioned -454 ~ -13 in the_end run scoreboard players set @s[r=177] currentBiome 3

# Ender Wilds
execute at @s positioned -200 ~ -80 in the_end run scoreboard players set @s[r=130] currentBiome 4
execute at @s positioned -110 ~ -223 in the_end run scoreboard players set @s[r=238] currentBiome 4
execute at @s positioned 163 ~ -130 in the_end run scoreboard players set @s[r=110] currentBiome 4
execute at @s positioned 109 ~ -340 in the_end run scoreboard players set @s[r=140] currentBiome 4

# Broken Sanctuary
execute at @s in the_end run scoreboard players set @s[x=333,dx=-116,y=0,dy=255,z=629,dz=-130] currentBiome 5
execute at @s in the_end run scoreboard players set @s[x=315,dx=164,y=0,dy=255,z=575,dz=114] currentBiome 5
execute at @s in the_end run scoreboard players set @s[x=422,dx=122,y=0,dy=255,z=528,dz=108] currentBiome 5
execute at @s in the_end run scoreboard players set @s[x=377,dx=66,y=0,dy=255,z=677,dz=163] currentBiome 5
execute at @s in the_end run scoreboard players set @s[x=223,dx=388,y=0,dy=255,z=832,dz=95] currentBiome 5
execute at @s in the_end run scoreboard players set @s[x=379,dx=84,y=0,dy=255,z=895,dz=166] currentBiome 5
# 443 677 377 840


#CLUSTERS
execute at @s in the_end run scoreboard players set @s[x=293,y=71,z=563,dx=46,dy=68,dz=36] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=330,y=59,z=629,dx=57,dy=60,dz=51] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=423,y=63,z=638,dx=45,dy=58,dz=38] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=477,y=66,z=570,dx=58,dy=51,dz=38] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=558,y=108,z=838,dx=42,dy=5,dz=56] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=227,y=108,z=840,dx=45,dy=5,dz=45] currentBiome 6
execute at @s in the_end run scoreboard players set @s[x=383,y=108,z=1013,dx=43,dy=5,dz=47] currentBiome 6

#INTERIORS
execute at @s in the_end run scoreboard players set @s[x=301,y=58,z=574,dx=14,dy=57,dz=14] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=338,y=64,z=641,dx=43,dy=17,dz=35] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=365,y=82,z=644,dx=14,dy=18,dz=14] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=461,y=45,z=566,dx=12,dy=47,dz=12] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=491,y=70,z=572,dx=34,dy=13,dz=36] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=426,y=71,z=641,dx=34,dy=16,dz=36] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=429,y=89,z=662,dx=12,dy=18,dz=12] currentBiome 7

execute at @s in the_end run scoreboard players set @s[x=584,y=110,z=848,dx=13,dy=23,dz=13] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=564,y=110,z=866,dx=15,dy=61,dz=15] currentBiome 7

execute at @s in the_end run scoreboard players set @s[x=410,y=110,z=1020,dx=14,dy=65,dz=14] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=430,y=120,z=1025,dx=13,dy=41,dz=13] currentBiome 7

execute at @s in the_end run scoreboard players set @s[x=256,y=110,z=866,dx=14,dy=60,dz=14] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=251,y=120,z=886,dx=14,dy=43,dz=14] currentBiome 7

execute at @s in the_end run scoreboard players set @s[x=379,y=67,z=859,dx=18,dy=45,dz=18] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=407,y=62,z=863,dx=20,dy=108,dz=20] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=399,y=170,z=855,dx=33,dy=23,dz=33] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=426,y=119,z=862,dx=11,dy=15,dz=13] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=425,y=59,z=893,dx=18,dy=41,dz=18] currentBiome 7
execute at @s in the_end run scoreboard players set @s[x=441,y=71,z=861,dx=12,dy=30,dz=12] currentBiome 7

execute at @s in the_end positioned 312 ~ -537 at @s[r=80] positioned ~ 111 ~ run scoreboard players set @s[dy=200] currentBiome 8

# Flying Fortresses
execute at @s in the_end positioned -569 58 217 run scoreboard players set @s[r=50] currentBiome 6
execute at @s in the_end positioned 355 74 86 run scoreboard players set @s[r=50] currentBiome 6
execute at @s in the_end positioned 503 81 -402 run scoreboard players set @s[r=50] currentBiome 6
execute at @s in the_end positioned -478 64 -392 run scoreboard players set @s[r=50] currentBiome 6

# Mob Spawn Exclusions
tag @s remove no_mob_spawning

# Excluding Cube Boss Arena
tag @s[x=312,y=223,z=-537,r=30] add no_mob_spawning
# Excluding area around the Ender Wilds spawnpoint
tag @s[x=-148,y=58,z=15,r=50] add no_mob_spawning


# Ender Dragon
execute at @s positioned -10 ~ 17 in the_end run scoreboard players set @s[r=70] currentBiome 9
execute at @s positioned 58 ~ -45 in the_end run scoreboard players set @s[r=50] currentBiome 9

scoreboard players operation lastBiome math = @s lastBiome
scoreboard players operation currentBiome math = @s currentBiome
scoreboard players operation currentBiome math -= lastBiome math

# currentBiome math not being 0 means that the player has switched the biome since last iteration 
execute unless score currentBiome math matches 0 if score @s lastBiome matches 1 run function biome/echoing_forest/leave
execute unless score currentBiome math matches 0 if score @s lastBiome matches 2 run function biome/ender_riftlands/leave
execute unless score currentBiome math matches 0 if score @s lastBiome matches 3 run function biome/ender_abyss/leave
execute unless score currentBiome math matches 0 if score @s lastBiome matches 4 run function biome/ender_wilds/leave
# the unless score at the end of this one just makes sure that we don't run a leave event
# when the player moves between the different broken sanctuary zones
# note that currentBiome math is different to @s currentBiome 
execute unless score currentBiome math matches 0 if score @s lastBiome matches 5..7 unless score @s currentBiome matches 5..7 run function biome/broken_sanctuary/leave
execute unless score currentBiome math matches 0 if score @s lastBiome matches 8 run function biome/ender_abyss_inner/leave

execute unless score currentBiome math matches 0 if score @s currentBiome matches 1 run function biome/echoing_forest/enter
execute unless score currentBiome math matches 0 if score @s currentBiome matches 2 run function biome/ender_riftlands/enter
execute unless score currentBiome math matches 0 if score @s currentBiome matches 3 run function biome/ender_abyss/enter
execute unless score currentBiome math matches 0 if score @s currentBiome matches 4 run function biome/ender_wilds/enter
execute unless score currentBiome math matches 0 if score @s currentBiome matches 5..7 unless score @s lastBiome matches 5..7 run function biome/broken_sanctuary/enter
execute unless score currentBiome math matches 0 if score @s currentBiome matches 8 run function biome/ender_abyss_inner/enter